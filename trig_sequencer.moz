@OnLoad
  ShowLayout 1
  SetMetroSwing 0

  ppqn = 384

  SetMetroPPQN ppqn

  LabelKnobs { }

  if Unassigned notes
    noteVal = 64
    for knob = 0 to 7
      Call @MySetNoteKnob
      knob = knob + 11
      Call @MySetNoteKnob
    endfor
  else
    for knob = 0 to 7
      noteVal = notes[knob]
      Call @MySetNoteKnob
      knob = knob + 11
      noteVal = notes[knob-3]
      Call @MySetNoteKnob
    endfor    
  endif

  if Unassigned maxStep
    LabelKnob 8, {Length:16} 
    SetKnobValue 8, 127
    maxStep = 16
  else
    LabelKnob 8, {Length:}, maxStep
    SetKnobValue 8, (maxStep - 2) / 14 * 127
  endif

  if Unassigned mutateProb
    mutateProb = 0
  endif   
  LabelKnob 9, {Mutate}
  SetKnobValue 9, mutateProb 
  
  if Unassigned gateLength
    gateLength = 1
  endif   
  LabelKnob 10, {Gate}
  SetKnobValue 10, (gateLength - 1)/500 * 127

  lastStep = maxStep - 1
  newStep = -1

  if Unassigned trigs
    for knob = 19 to 21
      LabelKnob knob, {-}
      SetKnobValue knob, 0
      trigs[knob-19] = 0
    endfor
  else
    for knob = 19 to 21
      trigVal = trigs[knob-19]
      SetKnobValue knob, trigVal / 16 * 127
      if trigVal = 0
        LabelKnob knob, {-}
      else
        LabelKnob knob, {1/}, trigVal
      endif
    endfor
  endif
  
  Call @MySetAllPulses
@End

@OnKnobChange
  knob = LastKnob
  knobVal = (GetKnobValue knob)

  if (knob >= 0 and knob <= 7) or (knob >= 11 and knob <= 18)
    noteVal = Round(knobVal)
    Call @MySetNoteKnob
  elseif knob = 8
    maxStep = Round(knobVal / 127 * 14) + 2
    LabelKnob knob, {Length:}, maxStep
  elseif knob = 9
    mutateProb = knobVal
  elseif knob = 10
    gateLength = knobVal / 127 * 500 + 1
  elseif knob >= 19 and knob <= 21
    trigVal = Round(knobVal / 127 * 16)
    trigs[knob-19] = trigVal
    if trigVal = 0
      LabelKnob knob, {-}
    else
      LabelKnob knob, {1/}, trigVal
    endif
    Call @MySetAllPulses    
  endif
@End

@MySetAllPulses
  for i = 0 to 1000
    pulses1[i] = 0
    pulses2[i] = 0
  endfor

  for i = 0 to 2
    v = trigs[i]
    if v > 0
      increment = ppqn * 4 / v
      floatPulse = 0.0
      while floatPulse < ppqn * 4
        pulse = Round floatPulse
        if pulse < 1000
          Inc pulses1[pulse]
        else
          Inc pulses2[pulse-1000]
        endif
        floatPulse = floatPulse + increment
      endwhile
    endif
  endfor
@End

@OnMetroPulse
  addStep = 0
  p = CurrentMetroPulse
  if p < 1000
    addStep = pulses1[p]
  else
    addStep = pulses2[p-1000]
  endif

  if addStep > 0
    if mutateProb > Random 0, 127
      newStep = Random 0, maxStep-1
    else 
      newStep = (lastStep + addStep) % maxStep
    endif
    Call @MySetNewStep
  endif
@End

@MySetNoteKnob
  LabelKnob knob, {âš«ï¸}, (NoteName noteVal, YES)
  if knob > 7
    notes[knob-3] = noteVal
  else
    notes[knob] = noteVal
  endif
@End

@MySetNewStep
  note = notes[newStep]
  SendMIDINoteOn 0, note, 100
  SendMIDINoteOff 0, note, 0, gateLength

  if newStep <> lastStep
    if newStep > 7
      LabelKnob newStep+3, {ðŸ”´}, (NoteName note, YES)
    else
      LabelKnob newStep, {ðŸ”´}, (NoteName note, YES)
    endif

    note = notes[lastStep]
    if lastStep > 7
      LabelKnob lastStep+3, {âš«ï¸}, (NoteName note, YES)
    else
      LabelKnob lastStep, {âš«ï¸}, (NoteName note, YES)
    endif
  
    lastStep = newStep
  endif
@End

@OnShiftDown
  recStep = 0
@End 

@OnMidiNoteOn
  if ShiftPressed 
    noteVal = MIDINote
    if recStep > 7
      knob = recStep + 3
    else
      knob = recStep
    endif
    SetKnobValue knob, noteVal 
    call @MySetNoteKnob
    recStep = (recStep + 1) % 16
  endif 
@End
