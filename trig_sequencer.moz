@OnLoad
  ShowLayout 1
  SetMetroSwing 0

  ppqn = 384
  SetMetroPPQN ppqn

  LabelKnobs { }

  if Unassigned notes
    for knob = 0 to 7
      noteVal = 64
      Call @MySetNoteKnob
    endfor
  else
    for knob = 0 to 7
      noteVal = notes[knob]
      Call @MySetNoteKnob
    endfor    
  endif

  if Unassigned maxStep
    LabelKnob 10, {Length:8} 
    SetKnobValue 10, 127
    maxStep = 8
  else
    LabelKnob 10, {Length:}, maxStep
    SetKnobValue 10, (maxStep - 2) / 6 * 127
  endif

  if Unassigned mutateProb
    mutateProb = 0
  endif   
  LabelKnob 14, {Mutate}
  SetKnobValue 14, mutateProb 
  
  if Unassigned gateLength
    gateLength = 1
  endif   
  LabelKnob 15, {Gate}
  SetKnobValue 15, (gateLength - 1)/500 * 127

  lastStep = maxStep - 1
  newStep = -1

  if Unassigned trigs
    for knob = 11 to 13
      LabelKnob knob, {-}
      SetKnobValue knob, 0
      trigs[knob-11] = 0
    endfor
  else
    for knob = 11 to 13
      trigVal = trigs[knob-11]
      SetKnobValue knob, trigVal / 16 * 127
      if trigVal = 0
        LabelKnob knob, {-}
      else
        LabelKnob knob, {1/}, trigVal
      endif
    endfor
  endif
  
  Call @MySetAllPulses

  LabelKnob 8, { }
  SetKnobValue 8, 0
  LabelKnob 9, { }
  SetKnobValue 9, 0
  LabelKnob 16, { }
  SetKnobValue 16, 0
  LabelKnob 17, { }
  SetKnobValue 17, 0
  LabelKnob 18, { }
  SetKnobValue 18, 0
  LabelKnob 19, { }
  SetKnobValue 19, 0
  LabelKnob 20, { }
  SetKnobValue 20, 0
  LabelKnob 21, { }
  SetKnobValue 21, 0

@End

@OnKnobChange
  knob = LastKnob
  knobVal = (GetKnobValue knob)

  if knob >= 0 and knob <= 7
    noteVal = Round(knobVal)
    Call @MySetNoteKnob
  elseif knob = 8 or knob = 9 or knob = 16 or knob = 17 or knob = 18 or knob = 19 or knob = 20 or knob = 21
    SetKnobValue knob, 0
  elseif knob = 10
    maxStep = Round(knobVal / 127 * 6) + 2
    LabelKnob knob, {Length:}, maxStep
  elseif knob >= 11 and knob <= 13
    trigVal = Round(knobVal / 127 * 16)
    trigs[knob-11] = trigVal
    if trigVal = 0
      LabelKnob knob, {-}
    else
      LabelKnob knob, {1/}, trigVal
    endif
    Call @MySetAllPulses    
  elseif knob = 14
    mutateProb = knobVal
  elseif knob = 15
    gateLength = knobVal / 127 * 500 + 1
  endif
@End

@MySetAllPulses
  for i = 0 to 1000
    pulses1[i] = 0
    pulses2[i] = 0
  endfor

  for i = 0 to 2
    v = trigs[i]
    if v > 0
      increment = ppqn * 4 / v
      floatPulse = 0.0
      while floatPulse < ppqn * 4
        pulse = Round floatPulse
        if pulse < 1000
          Inc pulses1[pulse]
        else
          Inc pulses2[pulse-1000]
        endif
        floatPulse = floatPulse + increment
      endwhile
    endif
  endfor
@End

@OnMetroPulse
  addStep = 0
  p = CurrentMetroPulse
  if p < 1000
    addStep = pulses1[p]
  else
    addStep = pulses2[p-1000]
  endif

  if addStep > 0
    if mutateProb > Random 0, 127
      newStep = Random 0, maxStep-1
    else 
      newStep = (lastStep + addStep) % maxStep
    endif
    Call @MySetNewStep
  endif
@End

@MySetNoteKnob
  LabelKnob knob, {‚ö´Ô∏è}, (NoteName noteVal, YES)
  notes[knob] = noteVal
@End

@MySetNewStep
  note = notes[newStep] 
  SendMIDINoteOn 0, note, 100
  SendMIDINoteOff 0, note, 0, gateLength

  if newStep <> lastStep
    LabelKnob newStep, {üî¥}, (NoteName note, YES)
    note = notes[lastStep]
    LabelKnob lastStep, {‚ö´Ô∏è}, (NoteName note, YES)
  
    lastStep = newStep
  endif
@End

@OnShiftDown
  recStep = 0
@End 

@OnMidiNoteOn
  if ShiftPressed 
    noteVal = MIDINote
    knob = recStep
    SetKnobValue knob, noteVal 
    call @MySetNoteKnob
    recStep = (recStep + 1) % 8
  endif 
@End
